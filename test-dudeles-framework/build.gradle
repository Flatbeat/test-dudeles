description = 'Test-Dudeles-Framework'

buildscript {
    repositories {
        jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
    }

    dependencies {
        classpath 'com.palantir:gradle-baseline-java:+'
		classpath 'com.palantir:jacoco-coverage:+'
		classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format:0.2-SNAPSHOT"
    }
}

apply from: script('dependencies')

allprojects {

	apply plugin: 'java'

    group = 'de.his.internal.testdudeles.api'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
		jcenter()
    }
	
}

subprojects {

	apply plugin: "com.github.sherter.google-java-format"
	apply plugin: 'com.palantir.baseline-checkstyle'
	apply plugin: 'com.palantir.baseline-eclipse'
	apply plugin: 'com.palantir.baseline-findbugs'
	apply plugin: 'com.palantir.jacoco-coverage'
	apply plugin: 'com.palantir.jacoco-full-report'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
	
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
	
	dependencies {
		compile deps.guava
	
    	testCompile deps.assertj
		testCompile deps.hamcrest
        testCompile deps.junit
		testCompile deps.mockito
    }
	
	jacocoCoverage {
		fileThreshold 0.5
	}
	
}

File script(String name) {
    project.file("${rootProject.projectDir}/gradle/${name}.gradle")
}

def libs = '../test-dudeles-eclipse-plugin/test-dudeles-framework-bundle/libs'

task deletePluginBundleLibs(type: Delete) {
    delete fileTree(libs) {
        include '**/*.jar'
    }
}

task copyLibJars(type: Copy, dependsOn: subprojects.jar) {
    from(subprojects.jar) 
    into project.file(libs)
}

task copyDepJars(type: Copy) {
    from(subprojects.configurations.runtime) 
    into project.file(libs)
}

task copyPluginBundleLibs(dependsOn: [copyLibJars, copyDepJars])

task build {
	dependsOn 'deletePluginBundleLibs'
	finalizedBy 'copyPluginBundleLibs'
}